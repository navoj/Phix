-- Arwen32Dib : Fast bitmap manipulation for Arwen (24-bit graphics).
-- CLIPBOARD LIBRARY

-- Copyright (C) 2004 Tommy Carlier
-- tommy.carlier@telenet.be
-- http://users.telenet.be/tommycarlier
--  Ported to Arwen by Pete Lomax

include a32dcore.ew

global procedure copyDibToClipboard(a32Dib dib)
-- Copies the bitmap to the Windows clipboard.
a32Dib0 new_dib
    if c_func(xOpenClipboard, {c_func(xGetActiveWindow, {})}) then
        if c_func(xEmptyClipboard, {}) then
            new_dib = copyDib(dib)
            if sequence(new_dib) then
                void = c_func(xSetClipboardData, {CF_BITMAP, new_dib[DibHandle]})
            end if
        end if
        c_proc(xCloseClipboard, {})
    end if
end procedure

global function getDibFromClipboard()
-- Gets a bitmap from the clipboard and returns it.
-- Returns 0 if no bitmap could be copied from the clipboard.
atom hBitmap, pBitmap, hDC
a32Dib0 dib
integer size, width, height
    dib = 0
    if c_func(xIsClipboardFormatAvailable, {CF_BITMAP})
    and c_func(xOpenClipboard, {c_func(xGetActiveWindow, {})}) then
        hBitmap = c_func(xGetClipboardData, {CF_BITMAP})
        if c_func(xGetObjectType,{hBitmap})!=OBJ_BITMAP then ?9/0 end if
        size = c_func(xGetObject, {hBitmap,0,0})
        if size!=sizeofstruct(BITMAP) then ?9/0 end if
        pBitmap = allocate(size)
        if c_func(xGetObject, {hBitmap, size, pBitmap})!=size then ?9/0 end if
        width = peek4u(pBitmap+BITMAP_bmWidth)
        height = peek4u(pBitmap+BITMAP_bmHeight)
        free(pBitmap)
        hDC = c_func(xCreateCompatibleDC, {NULL})
        void = c_func(xSelectObject, {hDC, hBitmap})
--DEV tryme:
-- if 1 then
--      dib = extractDib(hDC, 0, 0, width, height)
-- else
        dib = newDib(width, height)
        if sequence(dib) then copyToDib2(dib, hDC, 0, 0, 0, 0, width-1, height-1) end if
-- end if
        void = c_func(xDeleteDC,{hDC})
        c_proc(xCloseClipboard, {})
    end if
    return dib
end function
