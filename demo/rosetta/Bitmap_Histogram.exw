--
-- demo\rosetta\Bitmap_Histogram.exw
-- =================================
--
-- Author Pete Lomax, October 2015
--
--  Requires read_ppm() from [[Bitmap/Read_a_PPM_file#Phix|Read_a_PPM_file]], write_ppm() from [[Bitmap/Write_a_PPM_file#Phix|Write_a_PPM_file]]. 
--  Uses lena.ppm, which you will have to find/download to demo/rosetta yourself.
--  Included as demo\rosetta\Bitmap_Histogram.exw, results may be verified with demo\rosetta\viewppm.exw

-- Some colour constants:
constant black = #000000,
--       blue  = #0000FF,
--       green = #00FF00
--       red   = #FF0000,
--       red = 0xff2020,
--       yellow = 0xffdf20 --,
         white = #FFFFFF

-- copied from Bitmap_read_ppm:
function read_ppm(sequence filename)
sequence image, line
integer dimx, dimy, maxcolor
atom fn = open(filename, "rb")
    if fn<0 then
        return -1 -- unable to open
    end if
    line = gets(fn)
    if line!="P6\n" then
        return -1 -- only ppm6 files are supported
    end if
    line = gets(fn)
    {{dimx,dimy}} = scanf(line,"%d %d%s")
    line = gets(fn)
    {{maxcolor}} = scanf(line,"%d%s")
    image = repeat(repeat(0,dimy),dimx)
    for y=1 to dimy do
        for x=1 to dimx do
            image[x][y] = getc(fn)*#10000 + getc(fn)*#100 + getc(fn)
        end for
    end for
    close(fn)
    return image
end function

-- copied from Bitmap_write_ppm:
procedure write_ppm(sequence filename, sequence image)
integer fn,dimx,dimy
sequence colour_triple
    fn = open(filename,"wb")
    dimx = length(image)
    dimy = length(image[1])
    printf(fn, "P6\n%d %d\n255\n", {dimx,dimy})
    for y=1 to dimy do
        for x=1 to dimx do
            colour_triple = sq_div(sq_and_bits(image[x][y], {#FF0000,#FF00,#FF}),
                                                            {#010000,#0100,#01})
            puts(fn, colour_triple)
        end for
    end for
    close(fn)
end procedure

function to_bw(sequence image)
sequence color
integer lum
sequence hist = repeat(0,256) 
integer l = 1, r = 256
integer ltot, rtot
    for i=1 to length(image) do
        for j=1 to length(image[i]) do
            color = sq_div(sq_and_bits(image[i][j], {#FF0000,#FF00,#FF}),
                                                    {#010000,#0100,#01})
            lum = floor(0.2126*color[1] + 0.7152*color[2] + 0.0722*color[3])
            image[i][j] = lum
            hist[lum+1] += 1
        end for 
    end for 
    ltot = hist[l]
    rtot = hist[r]
    while l!=r do
        if ltot<rtot then
            l += 1
            ltot += hist[l]
        else
            r -= 1
            rtot += hist[r]
        end if
    end while
    lum = l
    for i=1 to length(image) do
        for j=1 to length(image[i]) do
            image[i][j] = iff(image[i][j]<lum?black:white)
        end for 
    end for 
    return image
end function

sequence img = read_ppm("Lena.ppm")
    img = to_bw(img)
    write_ppm("LenaBW.ppm",img)

--{} = wait_key()




